(*
Minimap to Mainscreen
=====================
The core for our minimap to mainscreen projection.
Supports rotation, and zoom, and resizable client.

MM2MS extends the Minimap interface as well as the mainscreen interface with 
functionality to convert coordinates to & from the minimap to the mainscreen.
*)

(*
MM2MS
-----
*)

(*
type TMinimapToMS
~~~~~~~~~~~~~~~~~
.. pascal:: 
  type
    TMinimapToMS = record
      BASE_W, BASE_H: Int32;        // the size of the area we project to (mainscreen size)
      MSCX, MSCY: Int32;            // the center of the area we project to (mainscreen center)
      MMCX, MMCY: Int32;            // the center of the area we project from (minimap center)
      SCALE_MIN, SCALE_MAX: Double; // direct zoom (relates to BASE)
      ZOOM: Double;                 // current zoom
      YSHIFT: Double;               // zooming may cause mid-Y to shift
    end;
   
The comments explains it. This is the datastructure MM2MS uses.
*)
type
  TMinimapToMS = record
    BASE_W, BASE_H: Int32;        // the size of the area we project to (mainscreen size)
    MSCX, MSCY: Int32;            // the center of the area we project to (mainscreen center)
    MMCX, MMCY: Int32;            // the center of the area we project from (minimap center)
    SCALE_MIN, SCALE_MAX: Double; // direct zoom (relates to BASE)
    ZOOM: Double;                 // current zoom
    YSHIFT: Double;               // zooming may cause mid-Y to shift
  end;


(*
var MM2MS
~~~~~~~~~
.. pascal::
  var MM2MS: TMinimapToMS;

The global variable which you would refer to.
*)
var
  MM2MS: TMinimapToMS;


(*
MM2MS.Init
~~~~~~~~~~
.. pascal:: procedure TMinimapToMS.Init(Resizable: Boolean);

This method is called automatically once SRL loads. So you don't have to deal with it.

This methods Init's the MM2MS variable, sets up values needed for a succesful projection.
MM2MS supports Resizable RS client, but SRL does not. This is added only for future-proofing.
*)
procedure TMinimapToMS.Init(Resizable: Boolean);
var
  B: TBox;
begin
  if not Resizable then
  begin
    Self := [
      513, 335,     // ms size
      259, 172,     // ms center
      642, 84,      // mm center
      0.436843666601926, 3.39, // scale min, scale max
      0.0,          // current zoom
      12            // zoom Y offset (imperfect)
    ];
  end else
  begin
    B := GetClientBounds();
    Self := [
      513, 335,
      B.Middle.X, B.Middle.Y,
      B.X2 - 81, B.Y1 + 84,    //this may needs tweaking if the minimap moves
      1.08 * B.Height / 503, 4.4 * B.Height / 503,
      0.0,
      24
    ];
  end;
end;


(*
MM2MS.Transform
~~~~~~~~~~~~~~~
.. pascal:: function TMinimapToMS.Transform(coord: Vector3; matrix: TMatrix4): Vector3;

Transforms the given coordinate with the given transformation matrix ``matrix``
*)
function TMinimapToMS.Transform(coord: Vector3; matrix: TMatrix4): Vector3;
var
  p: Vector3;
  scale: Double;
begin
  scale := SCALE_MIN + ZOOM * (SCALE_MAX-SCALE_MIN);
  p := Vector3.TransformCoordinate(coord, matrix);
  Result.x := (+p.X * BASE_W * scale) + MSCX;
  Result.y := (-p.Y * BASE_H * scale) + MSCY + (zoom*YSHIFT);
end;

(*
MM2MS.Project
~~~~~~~~~~~~~
.. pascal:: function TMinimapToMS.Project(Coords: Vector3Array; Rotation: Vector3): TPointArray;

Projects the given coordinates ``coo rds`` and applys the given ``Rotation`` vector to the projection.
*)
function TMinimapToMS.Project(Coords: Vector3Array; Rotation: Vector3): TPointArray;
var
  vec: Vector3;
  viewMatrix, projMatrix, worldMatrix,transMatrix: TMatrix4;
begin           //low angle test  [0,-28.5,13]
  viewMatrix := TMatrix4.LookAtRH([0, -19.3754622167442, 49.6649320817087], [0, 0, 0.448477682564408], Vector3_UnitY);
  projMatrix := TMatrix4.PerspectiveFovRH(Sqrt(2), 765 / 503, 0.01, 1.0);

  worldMatrix := TMatrix4.RotationYawPitchRoll(Rotation.Y, Rotation.X, Rotation.Z) * Matrix_Identity;
  transMatrix := worldMatrix * viewMatrix * projMatrix;
  for vec in Coords do
  begin
    vec := Transform(vec, transMatrix);
    Result += Point(Round(vec.x),Round(vec.y));
  end;
end;

(*
MM2MS.Run
~~~~~~~~~
.. pascal:: function TMinimapToMS.Run(Arr: Vector3Array; Roll:Single=$FFFF): TPointArray;

The method we actually call (internally) to project our coordinates from the minimap to the mainscreen.
*)
function TMinimapToMS.Run(Arr: Vector3Array; Roll:Single=$FFFF): TPointArray;
var
  i: Int32;
begin
  for i:=0 to High(Arr) do
    Arr[i] := [Arr[i].x - MMCX, (MMCY - Arr[i].y), Arr[i].z];
  
  Result := Project(Arr, Vec3(0, 0, PI*2 - Roll));
end;

procedure TMinimapToMS.SetZoomLevel(Level: Int32);

  procedure SetScale(Z, Y: Double);
  begin
    Self.ZOOM := Z;
    Self.YSHIFT := Y;
  end;

begin
  case Level of
    0: SetScale(0.0000, 28.0000);
    1: SetScale(0.0025, 28.0000);
    2: SetScale(0.0065, 28.0000);
    3: SetScale(0.0100, 28.0000);
    4: SetScale(0.0130, 28.0000);
    5: SetScale(0.0165, 28.0000);
    6: SetScale(0.0205, 28.0000);
    7: SetScale(0.0235, 28.0000);
    8: SetScale(0.0280, 28.0000);
    9: SetScale(0.0310, 23.7500);
    10: SetScale(0.0350, 20.0000);
    11: SetScale(0.0390, 20.2500);
    12: SetScale(0.0430, 17.2500);
    14: SetScale(0.0465, 17.7500);
    15: SetScale(0.0515, 17.5000);
    16: SetScale(0.0555, 16.2500);
    17: SetScale(0.0605, 18.2500);
    18: SetScale(0.0645, 14.5000);
    19: SetScale(0.0690, 15.0000);
    20: SetScale(0.0740, 18.2500);
    21: SetScale(0.0785, 16.0000);
    22: SetScale(0.0835, 14.0000);
    23: SetScale(0.0885, 16.0000);
    24: SetScale(0.0935, 19.2500);
    25: SetScale(0.0990, 18.5000);
    26: SetScale(0.1045, 19.5000);
    27: SetScale(0.1095, 17.0000);
    28: SetScale(0.1150, 18.0000);
    29: SetScale(0.1205, 16.7500);
    30: SetScale(0.1260, 16.0000);
    31: SetScale(0.1325, 18.0000);
    32: SetScale(0.1385, 21.2500);
    33: SetScale(0.1455, 20.7500);
    34: SetScale(0.1505, 17.7500);
    35: SetScale(0.1585, 18.2500);
    36: SetScale(0.1635, 18.2500);
    38: SetScale(0.1705, 18.7500);
    39: SetScale(0.1775, 19.5000);
    40: SetScale(0.1845, 19.5000);
    41: SetScale(0.1925, 20.7500);
    42: SetScale(0.1995, 20.7500);
    43: SetScale(0.2055, 21.7500);
    44: SetScale(0.2145, 18.5000);
    45: SetScale(0.2215, 20.0000);
    46: SetScale(0.2305, 19.7500);
    47: SetScale(0.2365, 19.7500);
    48: SetScale(0.2465, 20.7500);
    49: SetScale(0.2545, 20.7500);
    50: SetScale(0.2635, 21.5000);
    51: SetScale(0.2725, 22.2500);
    52: SetScale(0.2825, 22.0000);
    53: SetScale(0.2905, 20.5000);
    54: SetScale(0.3005, 20.7500);
    55: SetScale(0.3105, 21.5000);
    56: SetScale(0.3195, 23.5000);
    57: SetScale(0.3285, 22.5000);
    58: SetScale(0.3405, 22.7500);
    59: SetScale(0.3555, 22.0000);
    60: SetScale(0.3615, 22.5000);
    61: SetScale(0.3705, 22.0000);
    62: SetScale(0.3825, 22.2500);
    64: SetScale(0.3945, 22.7500);
    65: SetScale(0.4065, 23.0000);
    66: SetScale(0.4185, 22.7500);
    67: SetScale(0.4305, 24.5000);
    68: SetScale(0.4425, 23.2500);
    69: SetScale(0.4575, 23.5000);
    70: SetScale(0.4695, 24.2500);
    71: SetScale(0.4815, 24.2500);
    72: SetScale(0.4965, 23.5000);
    73: SetScale(0.5115, 24.5000);
    74: SetScale(0.5265, 24.7500);
    75: SetScale(0.5385, 24.5000);
    76: SetScale(0.5535, 25.5000);
    77: SetScale(0.5685, 25.5000);
    78: SetScale(0.5835, 24.7500);
    79: SetScale(0.5985, 25.0000);
    80: SetScale(0.6165, 25.0000);
    81: SetScale(0.6315, 25.5000);
    82: SetScale(0.6495, 26.2500);
    83: SetScale(0.6645, 27.2500);
    84: SetScale(0.6825, 26.0000);
    85: SetScale(0.7035, 26.2500);
    86: SetScale(0.7215, 27.0000);
    88: SetScale(0.7395, 26.7500);
    89: SetScale(0.7575, 27.2500);
    90: SetScale(0.7815, 27.0000);
    91: SetScale(0.7995, 27.5000);
    92: SetScale(0.8175, 26.5000);
    93: SetScale(0.8355, 27.2500);
    94: SetScale(0.8595, 28.5000);
    95: SetScale(0.8835, 27.7500);
    96: SetScale(0.9015, 27.7500);
    97: SetScale(0.9255, 28.7500);
    98: SetScale(0.9495, 29.0000);
    99: SetScale(0.9735, 28.2500);
    100: SetScale(1.000, 28.2500);
    else
      raise 'Zoom level ' + IntToStr(Level) + ' not supported';
  end;
end;

(*
MM2MS.SetZoom
~~~~~~~~~~~~~
.. pascal:: procedure TMinimapToMS.SetZoom(Notch: ERSZoomNotch); overload;

Changes the zoom level MM2MS operates at.

.. pascal::
  MM2MS.SetZoom(Options.GetZoomNotch());

*)
procedure TMinimapToMS.SetZoom(Notch: ERSZoomNotch); overload;
begin
  case Notch of
    ZOOM_NOTCH_0:   Self.SetZoomLevel(0);
    ZOOM_NOTCH_25:  Self.SetZoomLevel(25);
    ZOOM_NOTCH_50:  Self.SetZoomLevel(50);
    ZOOM_NOTCH_75:  Self.SetZoomLevel(75);
    ZOOM_NOTCH_100: Self.SetZoomLevel(100);
    else
      SRL.WriteLn('Unknown zoom notch', dtFatal);
  end;
end;

(*
MM2MS.SetZoom
~~~~~~~~~~~~~
.. pascal:: procedure TMinimapToMS.SetZoom; overload;

Sets up MM2MS for the current zoom level in game.

.. pascal::
  MM2MS.SetZoom();

*)
procedure TMinimapToMS.SetZoom; overload;
begin
  Self.SetZoomLevel(Options.GetZoom());
end;

{$IFNDEF CODEINSIGHT}
begin
  MM2MS.Init(False);
  MM2MS.SetZoom(ZOOM_NOTCH_DEFAULT);
end;
{$ENDIF}


(*
Minimap
------------------------------------------------------
Extend the minimap-functionality with MM2MS functions
*)

(*
Minimap.ArrToMs
~~~~~~~~~~~~~~~
.. pascal:: 
  function TRSMinimap.ArrToMs(Arr: Vector3Array; Roll:Single=$FFFF): TPointArray; overload;
  function TRSMinimap.ArrToMs(Arr: TPointArray; Roll:Single=$FFFF): TPointArray; overload;

Takes array of points on the minimap, returns mainscreen projected array of points.
The first method takes a vector3 array as input, so we can pass height as well, and more detailed coordinates to scale.
*)
function TRSMinimap.ArrToMs(Arr: Vector3Array; Roll:Single=$FFFF): TPointArray; overload;
var
  i: Int32;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  for i:=0 to High(Arr) do
    Arr[i] := Arr[i].RotateXY(PI*2 - Roll, MM2MS.MMCX, MM2MS.MMCY);
  
  Result := MM2MS.Run(Arr, Roll);
end;

function TRSMinimap.ArrToMs(Arr: TPointArray; Roll:Single=$FFFF): TPointArray; overload;
var
  VecArr: Vector3Array;
  i: Int32;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  
  SetLength(VecArr, Length(Arr));
  for i:=0 to High(Arr) do
  begin
    VecArr[i].x := Arr[i].x;
    VecArr[i].y := Arr[i].y;
    VecArr[i] := VecArr[i].RotateXY(PI*2 - Roll, MM2MS.MMCX, MM2MS.MMCY);
  end;
  Result := MM2MS.Run(VecArr, Roll);
end;

(*
Minimap.VecToMs
~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.VecToMs(Vec: Vector3; Roll:Single=$FFFF): TPoint;

Takes a single coordinate as a Vector3 on the minimap, and converts it to a point on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.VecToMs(Vec: Vector3; Roll:Single=$FFFF): TPoint;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  Vec := Vec.RotateXY(PI*2 - Roll, MM2MS.MMCX, MM2MS.MMCY);
  
  Result := MM2MS.Run([Vec], Roll)[0];
end;

(*
Minimap.PointToMs
~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.PointToMs(PT: TPoint; Roll:Single=$FFFF): TPoint;

Takes a single coordinate as a TPoint on the minimap, and converts it to a point on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.PointToMs(PT: TPoint; Roll:Single=$FFFF): TPoint;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  pt := pt.Rotate(PI*2 - Roll, Point(MM2MS.MMCX, MM2MS.MMCY));
  
  Result := MM2MS.Run([Vec3(PT.x, PT.y)], Roll)[0];
end;


(*
Minimap.VecToMsRect
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.VecToMsRect(Vec: Vector3; Roll:Single=$FFFF): TRectangle;

Takes a single coordinate as a Vector3 on the minimap, and converts it to a rectangle / tile on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.VecToMsRect(Vec: Vector3; Roll:Single=$FFFF): TRectangle;
var
  arr: TPointArray;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  Vec := Vec.RotateXY(PI*2 - Roll, MM2MS.MMCX, MM2MS.MMCY);

  Arr := MM2MS.Run([Vec3(Vec.x-2, Vec.y-2, Vec.z), Vec3(Vec.x+2, Vec.y-2, Vec.z), Vec3(Vec.x+2, Vec.y+2, Vec.z), Vec3(Vec.x-2, Vec.y+2, Vec.z)], Roll);
  Result := [Arr[0], Arr[1], Arr[2], Arr[3]];
end;

(*
Minimap.PointToMsRect
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.PointToMsRect(PT: TPoint; Roll:Single=$FFFF): TRectangle;

Takes a single coordinate as a TPoint on the minimap, and converts it to a rectangle / tile on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.PointToMsRect(PT: TPoint; Roll:Single=$FFFF): TRectangle;
var
  arr: TPointArray;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  pt := pt.Rotate(PI*2 - Roll, Point(MM2MS.MMCX, MM2MS.MMCY));
  
  Arr := MM2MS.Run([Vec3(PT.x-2, PT.y-2), Vec3(PT.x+2, PT.y-2), Vec3(PT.x+2, PT.y+2), Vec3(PT.x-2, PT.y+2)], Roll);
  Result := [Arr[0], Arr[1], Arr[2], Arr[3]];
end;

(*
Minimap.VecToMsBox
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.VecToMsBox(Vec: Vector3; Roll:Single=$FFFF): TBox;

Takes a single coordinate as a Vector3 on the minimap, and converts it to a rough box on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.VecToMsBox(Vec: Vector3; Roll:Single=$FFFF): TBox;
var
  arr: TPointArray;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  Vec := Vec.RotateXY(PI*2 - Roll, MM2MS.MMCX, MM2MS.MMCY);

  Arr := MM2MS.Run([Vec3(Vec.x-2, Vec.y-2, Vec.z), Vec3(Vec.x+2, Vec.y-2, Vec.z), Vec3(Vec.x+2, Vec.y+2, Vec.z), Vec3(Vec.x-2, Vec.y+2, Vec.z)], Roll);
  Result := GetTPABounds(arr);
end;

(*
Minimap.VecToMsBox
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.PointToMsBox(PT: TPoint; Roll:Single=$FFFF): TBox;

Takes a single coordinate as a TPoint on the minimap, and converts it to a rough box on the mainscreen.

*ROLL is the compass angle, by leaving it default it will gather the compass angle itself.*
*)
function TRSMinimap.PointToMsBox(PT: TPoint; Roll:Single=$FFFF): TBox;
var
  arr: TPointArray;
begin
  if (Roll = $FFFF) then Roll := Self.GetCompassAngle(False);
  PT := PT.Rotate(PI*2 - Roll, Point(MM2MS.MMCX, MM2MS.MMCY));

  Arr := MM2MS.Run([Vec3(PT.x-2, PT.y-2), Vec3(PT.x+2, PT.y-2), Vec3(PT.x+2, PT.y+2), Vec3(PT.x-2, PT.y+2)], Roll);
  Result := GetTPABounds(arr);
end;

(*
Minimap.StaticToMsRect
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.StaticToMsRect(StaticMMPoint: TPoint; Height:Int32=0): TRectangle;

Takes static minimap coordinate, rotates it to compass angle, and returns a rectangle on the mainscreen
The static point is therefor gathered at north, and it will rotate it as expected.
*)
function TRSMinimap.StaticToMsRect(StaticMMPoint: TPoint; Height:Int32=0): TRectangle;
var
  angle: Double;
begin
  angle := Minimap.GetCompassAngle(False);
  with StaticMMPoint.Rotate(angle, Point(MM2MS.MMCX, MM2MS.MMCY)) do
    Result := Minimap.VecToMSRect(Vec3(X,Y, Height), angle);
end;

(*
Minimap.StaticToMs
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.StaticToMs(StaticMMPoint: TPoint; Height:Int32=0): TPoint;

This is the same as the above, only that it returns a single point on the mainscreen.
*)
function TRSMinimap.StaticToMs(StaticMMPoint: TPoint; Height:Int32=0): TPoint;
var
  angle: Double;
begin
  angle := Minimap.GetCompassAngle(False);
  with StaticMMPoint.Rotate(angle, Point(MM2MS.MMCX, MM2MS.MMCY)) do
    Result := Minimap.VecToMS(Vec3(X,Y, Height), angle);
end;



(*
Mainscreen
-------------------------------------------------------
Extend the mainscreen-functionality with MS2MM function
*)

(*
Mainscreen.PointToMM
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.PointToMM(MS: TPoint; Height: Int32=0; Accuracy:Double=0.2): Vector3;

Takes a mainscreen point and converts it to a point on the minimap.

Returns a Vector3 which includes input height. Conversion to a TPoint if that's what you need is simply 
done by calling `.ToPoint` on the result.

**Example**

.. pascal::
  WriteLn Mainscreen.PointToMM(Point(250,140), 2);           // as a vector (more accurate)
  WriteLn Mainscreen.PointToMM(Point(250,140), 2).ToPoint(); // as a TPoint (lost accuracy)
*)
function TRSMainScreen.PointToMM(MS: TPoint; Height: Int32=0; Accuracy:Double=0.2): Vector3;
var
  a: Double := Minimap.GetCompassAngle(False);

  function FindNearest(Area, Step: Double; FromMM: Vector3; IsFinal: Boolean): Vector3;
  var
    i,c: Int32;
    TPA: TPointArray;
    arr,raw: Vector3Array;
    best: TPoint;
    x,y: Double;
  begin
    best := Point(-9999, -9999);
    x := FromMM.X-Area;
    while x <= FromMM.X+Area do
    begin
      y := FromMM.Y-Area;
      while y <= FromMM.Y+Area do
      begin
        raw += Vector3([x,y,height]);
        arr += raw[High(raw)].RotateXY(-a, MM2MS.MMCX, MM2MS.MMCY);
        y += Step;
      end;
      x += Step;
    end;

    TPA := Minimap.ArrToMs(Copy(arr), a);
    if not IsFinal then Swap(arr, raw);
    for i:=0 to High(TPA) do
      if Distance(TPA[i], MS) < Distance(best, MS) then
      begin
        best := TPA[i];
        Result := arr[i];
      end;
    Result.z := Height;
  end;

var
  area: Double;
begin
  area := 48;
  Result := FindNearest(area, area, Vec3(MM2MS.MMCX, MM2MS.MMCY), False);
  while area > accuracy do
  begin
    Result := FindNearest(area, area, Result, False);
    area /= 2;
  end;
  Result := FindNearest(area, accuracy, Result, True);
end;
